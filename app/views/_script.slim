coffee:
  $ = jQuery

  request = (method, url)->
    $.ajax
      type: method
      url: url
      dataType: 'json'

  renderTemplate = (id, data)->
    tmpl = _.template($("#template-#{id}").html())
    tmpl(data)

  window.Mgmt = class Mgmt
    ENDPOINT = '/mgmt'

    @request: (method, path)->
      request method, "#{ENDPOINT}/#{path}".replace(/\/\//g, '/')

  window.App = class App
    @fetchAll: ->
      new Promise (resolve, reject)->
        Mgmt.request('GET', '/apps')
          .done (res)->
            resolve res['applications'].map (app)->
              new App(app)
          .fail reject

    @fetch: (name)->
      new Promise (resolve, reject)->
        Mgmt.request('GET', "/apps/#{name}")
          .done (app)->
            resolve new App(app)
          .fail reject

    constructor: (detail)->
      @name = detail.name
      @desc = 'TODO: app description?'

    render: ->
      el = $(renderTemplate('app-view', @))
      link = if el.hasClass('cs-app-link') then el else el.find('.cs-app-link')
      link.attr('href', "/apps/#{@name}")
      el
